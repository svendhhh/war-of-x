<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
	layout="horizontal"
	creationComplete="init()"
	pageTitle="My Super Chat">
	
	<mx:Script>
    	<![CDATA[
    		import com.hansel.UpdateEntry;
    		import mx.controls.textClasses.TextRange;
    		import com.hansel.LoginEntry;
    		import flash.net.navigateToURL;
    		import com.hansel.ChatEntry;
    		import mx.rpc.events.ResultEvent;
    		import mx.collections.ArrayCollection;
    		import mx.controls.Alert;
    		import mx.rpc.events.FaultEvent; 
	
    		private var updateTimer:Timer = new Timer(1000, 1);
    		private var lastUpdateTime:Number = 0;
    		[Bindable]
    		private var latestUpdate:UpdateEntry;
    		private var logoutURL:String;

			public function updateFaultHandler(event:FaultEvent):void {				
				chatBox.htmlText += "<font color='#FF2222'>["+ new Date().toLocaleTimeString() +"] Error: Update failed.</font><br>";
				updateTimer.reset();
				updateTimer.start();
			}
			
			public function loginFaultHandler(event:FaultEvent):void {
				Alert.show( "Could not Login: " + event.fault);
			}
			
			public function init():void {
				chatSrv.loginURL();
			}
			
			public function updateChat(event:TimerEvent):void {
				chatSrv.getUpdate(lastUpdateTime);
			}
			
			public function send():void {
				if(chatIn.text.length==0)return;
				chatSrv.addChat(chatIn.text);
				chatIn.text='';	
			}
			
			public function loginHandler(event:ResultEvent):void {
				var loginEntry:LoginEntry = event.result as LoginEntry;
				if(loginEntry.isLoggedIn) {
					updateTimer.addEventListener(TimerEvent.TIMER,updateChat);
					updateTimer.start();
					chatPanel.visible=true;
					//lastUpdateTime = loginEntry.timestamp;
					logoutURL = loginEntry.logoutURL;
					
					chatBox.htmlText = "<font color='#33CC33'>"+loginEntry.greeting + "</font><br>";
					
				}
				else{
					navigateToURL(new URLRequest(loginEntry.redirectURL),'_self');
				}
			}
			
			public function logout():void {
				navigateToURL(new URLRequest(logoutURL),'_self');
			}

			public function updateHandler(event:ResultEvent):void {
				latestUpdate = event.result as UpdateEntry;
				for(var i:int=0; i<latestUpdate.chatUpdates.length; i++){
					var entry:ChatEntry = latestUpdate.chatUpdates[i];
					var date:Date = new Date(entry.timestamp);
					chatBox.htmlText += "<font color='#3333EE'>[" + date.toLocaleTimeString() 
									 + 	" " + entry.user.nickName + "]</font> " + entry.chatText + "<br>";
					lastUpdateTime = entry.timestamp;
				}
				updateTimer.reset();
				updateTimer.start();
			}
			
			public function scrollToBottom():void{
				chatBox.verticalScrollPosition = chatBox.maxVerticalScrollPosition;
			}
    		
    	]]>
    </mx:Script>
	
	<mx:RemoteObject id="chatSrv" destination="chatService">
    	 <mx:method name="getUpdate" result="updateHandler(event)" fault="updateFaultHandler(event)" />
    	 <mx:method name="loginURL" result="loginHandler(event)" fault="loginFaultHandler(event)" />
   	</mx:RemoteObject>
	
	<mx:Panel id="chatPanel" title="My Super Chat" layout="horizontal" horizontalGap="0" visible="false">
		<mx:VDividedBox height="360">
			<mx:TextArea width="380" height="310" editable="false" id="chatBox" borderThickness="1" borderStyle="solid" updateComplete="scrollToBottom()" />
			<mx:TextInput width="380" id="chatIn" enter="send()" />
		</mx:VDividedBox>
		<mx:VBox verticalGap="0">
		<mx:Button height="20" width="100" label="Logout"  cornerRadius="0" id="logoutButton" click="logout()"/>
			<mx:List width="100" height="300" borderStyle="solid" id="usersList" dataProvider="{latestUpdate.activeUsers}" />
			<mx:Button height="40" width="100" label="Send"  cornerRadius="0" id="sendButton" click="send()"/>
		</mx:VBox>
	</mx:Panel>
	
</mx:Application>
